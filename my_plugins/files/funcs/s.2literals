
local msg=$(cat <<EOF
note: this is for string literal patterns. for regex, use search.2patterns.pcre

This function searches files in the given directory for the two given patterns.
It can become memory intensive depending on the size of files, and it will fail
silently if there are insufficent resources during any search.

The arguments are:
	1. 'pattern1'
	2. 'pattern2'
	3. directory to search recursively

EOF
)

local pattern1="$1"
local pattern2="$2"
local foo=$(print $3(:a))

if (( $# != 3 )) {
	print $msg
	return
} elif [[ ! -d $foo ]] {
	echo "no directory $3 found\n"
	print $msg
	return
} else {
	zmodload zsh/mapfile
	print -l $foo/**/*(DN.e{'z=$mapfile[$REPLY] && [[ $z = *$pattern1* && $z = *$pattern2* ]]'})
}

# vim: ft=zsh:
